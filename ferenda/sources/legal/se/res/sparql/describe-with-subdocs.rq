PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rpubl: <http://rinfo.lagrummet.se/ns/2008/11/rinfo/publ#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX olo: <http://purl.org/ontology/olo/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>


CONSTRUCT {
  ?target dcterms:isReferencedBy ?referer .
  ?referer dcterms:isPartOf ?rootdoc ;
           dcterms:identifier ?refid ;
           dcterms:title ?reftitle ;
           bibo:chapter ?refchapter ;
           rdfs:label ?reflabel ;
           dcterms:creator ?refcreator . 
  ?rootdoc dcterms:identifier ?rootid ;
           dcterms:title ?roottitle .
}
WHERE
{
  ?referer dcterms:references ?target ;
           dcterms:isPartOf* ?rootdoc . 
  ?rootdoc dcterms:identifier ?rootid .
  OPTIONAL { ?referer dcterms:identifier ?refid . }
  OPTIONAL { ?referer dcterms:title ?reftitle . }
  OPTIONAL { ?referer bibo:chapter ?refchapter . }
  OPTIONAL { ?referer dcterms:creator ?refcreator . }
  OPTIONAL { ?referer rdfs:label ?reflabel . }
  OPTIONAL { ?rootdoc dcterms:title ?roottitle . }
  FILTER(STRSTARTS(STR(?target), "%(uri)s#"))
}
