# -*- coding: iso-8859-1 -*-
# ------------ forarbeten.ebnf ----------------

# FÖRARBETEN - propositioner, utskottsbetänkanden,
# regeringsskrivelser, EU-rättsakter (endast CELEX-nummer), SOU:er och
# Ds:ar

forarbeteref          ::= (ForarbRefs/ForarbRef/ExternalAvsnittRefs/ExternalAvsnittRef/AvsnittRefs/AnonPropRefs)

# exv "2007/08" eller "1999/2000"
RiksmoteID       ::= LangtAr, (Slash, Ar)?

ForarbRef ::= (PropRef/BetRef/SkrivelseRef/CelexRef/SouRef/DsRef)
ForarbRefs ::= ForarbRef, Sidor

# Propositioner exv "Prop. 1999/2000:100"
PropRef	       ::= PropPrefix, Whitespace, PropRefID
PropPrefix     ::= ('prop.'/'Prop.')
PropRefID      ::= RiksmoteID, Colon, LopnrID
LopnrID        ::= number

# exv "a. prop. s. 42"
AnonPropRefs ::= "a. prop.", Sidor

# Betänkanden exv "bet. 1998/99:Ju12"
BetRef         ::= BetPrefix, Whitespace, BetRefID
BetPrefix      ::= 'bet.'
BetRefID       ::= RiksmoteID, Colon, BetNumID
BetNumID       ::= char, char, char?, number

# Riksdagsskrivelser exv "rskr. 1992/93:17"
SkrivelseRef   ::= SkrivelsePrefix, Whitespace, SkrivelseRefID
SkrivelsePrefix::= 'rskr.'
SkrivelseRefID ::= RiksmoteID, Colon, LopnrID

# CELEX-hänvisningar
CelexRef       ::= CelexRefID
CelexRefID     ::= '3', digit, digit, (digit, digit)?, 'L', digit, digit, digit, digit

# Sida-hänvisningar ("s. 154", ", s. 154")
SidaRef       ::= Comma?, Whitespace, 's', Period?, Whitespace, SidaRefID
# "123 och 135", "123-125, 140-143", 
Sidor ::= SidaRef, (Hyphen, SidaRefID)?, (CommaSpace/WAndOrW, SidaRefID, (Hyphen, SidaRefID)?)*

SidaRefID ::= number

# SOU:er exv "SOU 2000:2, s. 183"
SouRef ::= SouPrefix, Whitespace, SouRefID
SouPrefix ::= "SOU"
SouRefID ::= RiksmoteID, Colon, LopnrID

# Ds:er exv "Ds 1997:29, s. 112"
DsRef ::= DsPrefix, Whitespace, DsRefID
DsPrefix ::= "Ds"
DsRefID ::= RiksmoteID, Colon, LopnrID


# Avsnitt ("3", "3.2",  "3.2.1", or "3.2.1.4")
AvsnittRefID    ::= digit+, (Period, digit+, (Period, digit+, (Period, digit+)?)?)?
AvsnittRef      ::= 'avsnitt', Whitespace, AvsnittRefID
AvsnittRefs     ::= AvsnittRef, (CommaSpace/WAndOrW, AvsnittRefID)*
ExternalBetRef  ::= Whitespace, 'i', Whitespace, 'kommitténs', Whitespace, 'betänkande'
ExternalAvsnittRef  ::= AvsnittRef, ExternalBetRef
ExternalAvsnittRefs ::= AvsnittRefs, ExternalBetRef

# ------------ end of forarbeten.ebnf ----------------
